---
id: eql
title: EQL
---

Using the EQL interface, you can request Pathom to fetch a specific shape of data.

If you are not familiar with EQL, check https://edn-query-language.org for an overview of the syntax.

The goal of using EQL is to express some data shape (hierarchy) without the values and
let Pathom fill in the values.

Using EQL is also the most efficient way to request multiple things at once with Pathom.
With EQL, Pathom knows the full request ahead of time. Therefore, Pathom can use this information
to optimize the planning and execution.

## Using EQL interface

Keep in mind that EQL is about expressing some data hierarchy, to start simple we will
use a flat structure to demonstrate the basic usage of the EQL interface:

```clojure
(ns com.wsscode.pathom.docs.eql-demos
  (:require [com.wsscode.pathom3.connect.built-in.resolvers :as pbir]
            [com.wsscode.pathom3.connect.indexes :as pci]
            [com.wsscode.pathom3.interface.eql :as p.eql]))

(def indexes
  (pci/register
    [(pbir/constantly-resolver ::pi 3.1415)
     (pbir/single-attr-resolver ::pi ::tau #(* % 2))]))

(p.eql/process indexes [::pi ::tau])
; => {::pi 3.1415 ::tau 6.283}
```

### Nested entities

Using [EQL joins](https://edn-query-language.org/eql/1.0.0/specification.html#_joins) you
can make specific requirements about nested data. In this example we will simulate the
existence of many worlds where PI have different values:

```clojure
(def indexes
  (pci/register
    [(pbir/constantly-resolver ::pi 3.1415)
     (pbir/single-attr-resolver ::pi ::tau #(* % 2))
     ; define a resolver to provide a collection of items
     (pbir/constantly-resolver ::pi-worlds
       [{::pi 3.14}
        {::pi 3.14159}
        {::pi 6.8}
        {::tau 20}
        {::pi 10 ::tau 50}])]))

(p.eql/process indexes
  ; using a map we are able to specify nested requirements from some attribute
  [{::pi-worlds [::tau ::pi]}])
; => {::pi-worlds
;      [{::tau 6.28
;        ::pi  3.14}
;       {::tau 6.28318
;        ::pi  3.14159}
;       {::tau 13.6
;        ::pi  6.8}
;       {::tau 20
;        ::pi  3.1415}
;       {::tau 50
;        ::pi  10}]}
```

### Providing map data

You can provide initial data to the EQL process using the following syntax:

```clojure
(p.eql/process indexes {::pi 2.3} [::tau])
; => {::tau 4.6}
```

### Providing data via EQL idents

Pathom uses the EQL `ident` as a form to specify a single attribute to start requesting
data from. Here is an example using the revolvers we created before:

```clojure
(p.eql/process indexes [{[::pi 2.3] [::tau]}])
; => {[::pi 2.3] {::tau 4.6}}
```

In this example, given `PI` is `2.3`, `Tau` becomes `4.6`, since it's defined as the
double of PI.

### Providing data with placeholders

You can use placeholders to provide in-query data for Pathom processing. To do this, lets
get back to our famous full name example, the way to provide data is to send it to
a placeholder key as [EQL parameters](https://edn-query-language.org/eql/1.0.0/specification.html#_parameters):

```clojure
(pco/defresolver full-name [{::keys [first-name last-name]}]
  {::full-name (str first-name " " last-name)})

(def env (pci/register full-name))

(p.eql/process env
  [{'(:>/bret {::first-name "Bret" ::last-name "Victor"})
    [::full-name]}])
; => {:>/bret {:com.wsscode.pathom3.docs.placeholder/full-name "Bret Victor"}}
```

When moving to a placeholder context, Pathom inherits the same parent data and merges
the params data to it, to illustrate let's make a nested example of it:

```clojure
(p.eql/process env
  [{'(:>/bret {::first-name "Bret" ::last-name "Victor"})
    [::full-name
     {'(:>/bard {::first-name "Bard"})
      [::full-name]}]}])
; {:>/bret
;   {:com.wsscode.pathom3.docs.placeholder/full-name "Bret Victor",
;    :>/bard
;    {:com.wsscode.pathom3.docs.placeholder/full-name "Bard Victor"}}}
```

:::note
Placeholders data parameters is a new feature of Pathom 3, not available in Pathom 2.
:::

### Union queries

Union queries provide a way to archive polymorphism in with EQL, for a review on the
union syntax refer to the [EQL Union specification page](https://edn-query-language.org/eql/1.0.0/specification.html#_unions).

Consider you want to request information for some user feed. In our feed example, there
are three types of entries: posts, ads and videos. Each type requires different attributes
to render. This is how we can write some resolvers to fetch each type:

```clojure
(def union-env
  (pci/register
    [(pbir/static-table-resolver `posts :acme.post/id
       {1 {:acme.post/text "Foo"}})
     (pbir/static-table-resolver `ads :acme.ad/id
       {1 {:acme.ad/backlink "http://marketing.is-bad.com"
           :acme.ad/title    "Promotion thing"}})
     (pbir/static-table-resolver `videos :acme.video/id
       {1 {:acme.video/title "Some video"}})
     (pbir/constantly-resolver :acme/feed
       [{:acme.post/id 1}
        {:acme.ad/id 1}
        {:acme.video/id 1}])]))

(p.eql/process union-env
  [{:acme/feed
    {:acme.post/id  [:acme.post/text]
     :acme.ad/id    [:acme.ad/backlink :acme.ad/title]
     :acme.video/id [:acme.video/title]}}])
; => {:acme/feed
;     [{:acme.post/text "Foo"}
;      {:acme.ad/backlink "http://marketing.site.com",
;       :acme.ad/title "Promotion thing"}
;      {:acme.video/title "Some video"}]}
```

To decide which path to take, Pathom looks if the entry data contains the key mentioned
in the union entry key. When they match Pathom picks that path option.

## Fetching run status

To debug a EQL process, look in the metadata of the result to get the run stats:

```clojure
(-> (p.eql/process indexes [::tau]) meta)
; =>
{:com.wsscode.pathom3.connect.runner/run-stats
 {:com.wsscode.pathom3.connect.planner/index-attrs
  {:com.wsscode.pathom3.docs.eql-demos/pi  2,
   :com.wsscode.pathom3.docs.eql-demos/tau 1},
  :com.wsscode.pathom3.connect.planner/unreachable-attrs
  #{},
  :com.wsscode.pathom3.connect.runner/compute-plan-run-finish-ms
  1.632031796479513E9,
  :com.wsscode.pathom3.connect.runner/graph-run-finish-ms
  1.632031796589152E9,
  :com.wsscode.pathom3.connect.runner/compute-plan-run-start-ms
  1.6320317961298E9,
  :com.wsscode.pathom3.connect.planner/root
  2,
  :com.wsscode.pathom3.connect.runner/node-run-stats
  {2
   {:com.wsscode.pathom3.connect.runner/run-start-ms
    1.632031796514809E9,
    :com.wsscode.pathom3.connect.runner/run-finish-ms
    1.632031796520259E9,
    :com.wsscode.pathom3.connect.runner/node-run-input
    {},
    :com.wsscode.pathom3.connect.runner/node-run-output
    {:com.wsscode.pathom3.docs.eql-demos/pi 3.1415}},
   1
   {:com.wsscode.pathom3.connect.runner/run-start-ms
    1.632031796555155E9,
    :com.wsscode.pathom3.connect.runner/run-finish-ms
    1.632031796567302E9,
    :com.wsscode.pathom3.connect.runner/node-run-input
    {:com.wsscode.pathom3.docs.eql-demos/pi 3.1415},
    :com.wsscode.pathom3.connect.runner/node-run-output
    {:com.wsscode.pathom3.docs.eql-demos/tau 6.283}}},
  :com.wsscode.pathom3.connect.planner/index-ast
  {:com.wsscode.pathom3.docs.eql-demos/tau
   {:type         :prop,
    :dispatch-key :com.wsscode.pathom3.docs.eql-demos/tau,
    :key          :com.wsscode.pathom3.docs.eql-demos/tau}},
  :com.wsscode.pathom3.connect.planner/unreachable-resolvers
  #{},
  :com.wsscode.pathom3.connect.runner/graph-run-start-ms
  1.632031796081473E9,
  :com.wsscode.pathom3.connect.planner/index-resolver->nodes
  {com.wsscode.pathom3.docs.eql_demos_SLASH_pi->com.wsscode.pathom3.docs.eql_demos_SLASH_tau-single-attr-transform
   #{1},
   com.wsscode.pathom3.docs.eql_demos_SLASH_pi-constant
   #{2}},
  :com.wsscode.pathom3.connect.planner/nodes
  {1
   {:com.wsscode.pathom3.connect.operation/op-name
    com.wsscode.pathom3.docs.eql_demos_SLASH_pi->com.wsscode.pathom3.docs.eql_demos_SLASH_tau-single-attr-transform,
    :com.wsscode.pathom3.connect.planner/node-id
    1,
    :com.wsscode.pathom3.connect.planner/expects
    {:com.wsscode.pathom3.docs.eql-demos/tau {}},
    :com.wsscode.pathom3.connect.planner/input
    {:com.wsscode.pathom3.docs.eql-demos/pi {}},
    :com.wsscode.pathom3.connect.planner/node-parents
    #{2},
    :com.wsscode.pathom3.connect.planner/source-for-attrs
    #{:com.wsscode.pathom3.docs.eql-demos/tau}},
   2
   {:com.wsscode.pathom3.connect.operation/op-name
    com.wsscode.pathom3.docs.eql_demos_SLASH_pi-constant,
    :com.wsscode.pathom3.connect.planner/node-id
    2,
    :com.wsscode.pathom3.connect.planner/expects
    {:com.wsscode.pathom3.docs.eql-demos/pi {}},
    :com.wsscode.pathom3.connect.planner/input
    {},
    :com.wsscode.pathom3.connect.planner/source-for-attrs
    #{:com.wsscode.pathom3.docs.eql-demos/pi},
    :com.wsscode.pathom3.connect.planner/run-next
    1}}}}
```

For more details on what is the `run-stats` check at the [runner page](runner.mdx#run-stats).
