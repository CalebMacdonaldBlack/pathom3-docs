---
id: smart-maps
title: Smart Maps
---

Smart maps are a custom map type powered by Pathom resolvers.

With smart maps you can leverage the power of Pathom processing using the accessible
standard Clojure map interfaces.

## Using smart maps

To create a smart map, we need the indexes with resolvers, here is a basic example:

```clojure
(ns com.wsscode.pathom.docs.smart-maps-demo
  (:require [com.wsscode.pathom3.connect.indexes :as pci]
            [com.wsscode.pathom3.connect.operation :as pco]
            [com.wsscode.pathom3.interface.smart-map :as psm]))

(pco/defresolver full-name [{::keys [first-name last-name]}]
  ::full-name (str first-name " " last-name))

(def indexes (pci/register full-name))

(def person-data {::first-name "Anne" ::last-name "Frank"})

(def smart-map (psm/smart-map indexes person-data))

; if you lookup for a key in the initial data, works the same way as a regular map
(::first-name smart-map) ; => "Anne"

; but when you read something that's not there, it will trigger tha Pathom engine to
; fulfill the attribute
(::full-name smart-map) ; => "Anne Frank"
```

When you start a smart map, Pathom creates an atom with the initial data, and then when
you request some new information Pathom triggers the resolver engine, merges the result
in the same atom and returns the value for that key, effectively caching it.

This way the subsequent accesses have the same speed as a local entry.

### Nested entities

Nested map values are wrapped with a smart map using the same environment configuration. For example:

```clojure
(ns com.wsscode.pathom.docs.smart-maps-demo
  (:require [com.wsscode.pathom3.connect.indexes :as pci]
            [com.wsscode.pathom3.connect.operation :as pco]
            [com.wsscode.pathom3.interface.smart-map :as psm]))

(pco/defresolver full-name [{::keys [first-name last-name]}]
  ::full-name (str first-name " " last-name))

(pco/defresolver anne []
  ::anne {::first-name "Anne" ::last-name "Frank"})

(def indexes (pci/register [full-name anne]))

(def smart-map (psm/smart-map indexes))

(::anne smart-map) ; => {::first-name "Anne" ::last-name "Frank"}

; nested access
(-> smart-map ::anne ::full-name) ; => "Anne Frank"
```

:::important
This only applies for native Clojure/script maps, it doesn't wraps records and other
custom map types.
:::

### Nested sequences

It also applies for maps inside sequences:

```clojure
(ns com.wsscode.pathom.docs.smart-maps-demo
  (:require [com.wsscode.pathom3.connect.indexes :as pci]
            [com.wsscode.pathom3.connect.operation :as pco]
            [com.wsscode.pathom3.interface.smart-map :as psm]))

(pco/defresolver full-name [{::keys [first-name last-name]}]
  ::full-name (str first-name " " last-name))

(pco/defresolver stars []
  ::star-wars-characters
  [{::first-name "Luke" ::last-name "Skywalker"}
   {::first-name "Darth" ::last-name "Vader"}
   {::first-name "Han" ::last-name "Solo"}])

(def indexes (pci/register [full-name stars]))

(def smart-map (psm/smart-map indexes))

; nested access on sequences
(mapv ::full-name (::star-wars-characters smart-map))
; => ["Luke Skywalker"
;     "Darth Vader"
;     "Han Solo"]
```

:::note
Smart maps do the conversion of sequence items to smart maps on the sequence read. This
means that anytime you read a sequence on a smart map, if the sequence is a `vector`, that
scan will be done ahead of time, otherwise it will use `map` and the processing is lazy.
:::

## Changes to smart maps

You can use the change operations of map in the smart map (`assoc`, `dissoc`, ...).

When a change operation happens, you get a new smart map. Be aware this new smart map
doesn't have the cached data from resolvers from the previous one. The modification is
done from the `source map`, the one used to create the smart map in the first place.

This is important so any data computed by resolvers can react to the changes based on
the new data context. The following example illustrates this behavior:

```clojure
(ns com.wsscode.pathom.docs.smart-maps-demo
  (:require [com.wsscode.pathom3.connect.indexes :as pci]
            [com.wsscode.pathom3.connect.operation :as pco]
            [com.wsscode.pathom3.interface.smart-map :as psm]))

(pco/defresolver full-name [{::keys [first-name last-name]}]
  ::full-name (str first-name " " last-name))

(def indexes (pci/register full-name))

(def person-data {::first-name "John" ::last-name "Lock"})

(def smart-map (psm/smart-map indexes person-data))

(::full-name smart-map) ; => "John Lock"

(-> smart-map
    (assoc ::last-name "Oliver")
    ::full-name)
; => "John Oliver", the full-name gets re-computed due to the change
```

## Keys modes

Smart Maps keys mode is a configuration to decide how a smart map respond to `(keys smart-map)`

To change this, use the helper `(psm/with-keys-mode ...)`, these are the available options
for it:

- `::psm/keys-mode-cached` - the default option, `keys` will return the keys cached
in the internal smart map atom.
- `::psm/keys-mode-reachable` - `keys` will return all possible keys that are reachable
from the current data and the index.

:::caution
Be careful with `::psm/keys-mode-reachable`, consider that depending on the index and
the current data, a simple `print` of the smart map can lead to infinite loops due
to the recursive properties of smart maps.
:::

## Options reference

`::psm/keys-modes`

## Overriding config
